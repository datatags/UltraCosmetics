plugins {
    // https://github.com/GradleUp/shadow
    id 'com.gradleup.shadow' version '8.3.5'
    // https://github.com/Minecrell/plugin-yml
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

dependencies {
    // Depend on a jar in the 'core' folder: fileTree(dir: '.', include: ['*.jar'])
    // https://hub.spigotmc.org/stash/projects/SPIGOT/repos/spigot/browse
    compileOnly 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
    // https://github.com/libraryaddict/LibsDisguises/
    compileOnly('LibsDisguises:LibsDisguises:10.0.44') {
        exclude group: 'org.spigotmc'
        exclude group: 'com.comphenix.protocol'
    }
    // https://github.com/MilkBowl/VaultAPI
    compileOnly('net.milkbowl.vault:VaultAPI:1.7') {
        exclude group: 'org.bukkit'
    }
    // https://github.com/PlaceholderAPI/PlaceholderAPI
    compileOnly 'me.clip:placeholderapi:2.11.6'
    // https://github.com/Rosewood-Development/PlayerPoints
    compileOnly 'org.black_ixx:playerpoints:3.3.0'
    // https://github.com/EngineHub/WorldGuard
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9'
    compileOnly 'com.mojang:authlib:2.3.31'
    // https://mvnrepository.com/artifact/com.googlecode.json-simple/json-simple
    compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
    // https://github.com/CryptoMorin/XSeries
    library 'com.github.cryptomorin:XSeries:13.0.0'
    // https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.1.0'
    // https://github.com/LuckPerms/LuckPerms
    compileOnly 'net.luckperms:api:5.4'
    // https://github.com/ArcanePlugins/Treasury
    compileOnly 'me.lokka30:treasury-api:2.0.2-SNAPSHOT'
    // https://github.com/JEFF-Media-GbR/Spigot-ChestSortAPI
    compileOnly 'de.jeff_media:ChestSortAPI:13.0.0-SNAPSHOT'
    // https://github.com/TownyAdvanced/Towny
    compileOnly 'com.palmergames.bukkit.towny:towny:0.100.4.7' // future versions only support Java 21
    // https://github.com/brettwooldridge/HikariCP
    library 'com.zaxxer:HikariCP:6.2.1'
    // https://github.com/WesJD/AnvilGUI
    implementation 'net.wesjd:anvilgui:1.10.4-SNAPSHOT'
    // https://github.com/datatags/MobChipLite
    implementation 'com.github.datatags.MobChipLite:mobchip-bukkit:1.10.9'
    //implementation fileTree(dir: '.', include: ['mobchip-*.jar']) // keep for testing purposes
    // https://github.com/DiscordSRV/DiscordSRV
    compileOnly 'com.discordsrv:discordsrv:1.29.0'
    // https://github.com/SoKnight/PEconomy
    compileOnly 'me.soknight:peconomy:2.7.4-pre1'
    // https://github.com/KyoriPowered/adventure
    implementation 'net.kyori:adventure-text-minimessage:4.18.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.4'
    implementation 'net.kyori:adventure-text-serializer-plain:4.18.0'
    implementation 'net.kyori:adventure-api:4.18.0'
    // https://github.com/TechnicallyCoded/FoliaLib
    implementation 'com.github.technicallycoded:FoliaLib:0.4.3'
    //https://www.spigotmc.org/resources/%E2%AD%90-player-auctions-%E2%AD%90-%E2%9E%A2-let-your-players-sell-items-1-7-1-20-4.83073/
    compileOnly 'com.olziedev:playerauctions-api:1.31.0-pre10'

    compileOnly files('libs/CoinsEngine-2.1.1.jar')
    compileOnly files('libs/NexEngine.jar')
    // Shaded by root project
    compileOnly(project(':legacy-events'))
}

shadowJar {
    // Causes shadow jar to overwrite normal jar, which makes it work with Jitpack
    archiveClassifier = ''
    // removes unused classes in 'implementation' path dependencies
    minimize {
        // Loads classes reflectively
        exclude(dependency('net.wesjd:anvilgui:.*'))
        exclude(dependency('com.github.technicallycoded:FoliaLib:.*'))
        exclude(dependency('com.github.datatags.MobChipLite:.*:.*'))
    }
    def path = 'be.isach.ultracosmetics.shaded'
    relocate('org.bstats', path + '.metrics')
    relocate('net.wesjd', path + '.anvilgui')
    relocate('me.gamercoder215.mobchip', path + '.mobchip')
    relocate('net.kyori', path + '.kyori')
    relocate('com.tcoded', path + '.tcoded')
}

bukkit {
    name = 'UltraCosmetics'
    authors = ['datatags']
    main = 'be.isach.ultracosmetics.UltraCosmetics'
    apiVersion = '1.17'
    softDepend = ['LibsDisguises', 'Vault', 'PlaceholderAPI', 'PlayerPoints', 'WorldGuard', 'Essentials', 'LuckPerms', 'DiscordSRV', 'ChestSort', 'Towny', 'PEconomy', 'PlayerAuctions', 'CoinsEngine', 'Treasury']
    commands {
        ultracosmetics {
            aliases = ['uc', 'cosmetics']
            description = 'Main command of UltraCosmetics'
            usage = '/<command>'
        }
    }
    foliaSupported = true
    permissions {
        'ultracosmetics.openmenu' {
            setDefault('TRUE')
        }
        'ultracosmetics.receivechest' {
            setDefault('TRUE')
        }
        'ultracosmetics.treasurechests.buykey' {
            setDefault('TRUE')
        }
        'ultracosmetics.bypass.disabledcommands' {
            setDefault('OP')
        }
        'ultracosmetics.bypass.cooldown' {
            setDefault('FALSE')
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            groupId project.group
            //artifactId project.name
            version project.version
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
